#!/bin/zsh -eu

command=$0:t
this_script="$(realpath $0)"
PROJECT=$this_script:h
cd $PROJECT

# right side main screen
declare -A aw3225qf=(
  [name]="AW3225QF"
  [bounds_5k]="{0, 0, 2560, 1440}"
  [bounds_4k]="{0, 0, 1920, 1080}"
)

# left side sub screen
declare -A s3221qs=(
    [name]="DELL S3221QS"
    [bounds_5k]="{0, 0, -2560, 1440}"
    [bounds_4k]="{0, 0, -1920, 1080}"
)

icc_aw3225qf=/Library/ColorSync/Profiles/Displays/AW3225QF-D61FFD6E-1D7D-42D9-A314-59D2ED731496.icc
icc_dci_p3="/System/Library/ColorSync/Profiles/DCI(P3) RGB.icc"

icc_aw3225qf_lldv="${HOME}/Library/ColorSync/Profiles/AW3225QF LLDV.icc"
icc_aw3225qf_standard="${HOME}/Library/ColorSync/Profiles/AW3225QF Standard.icc"
icc_aw3225qf_edid_strict_v2="${HOME}/Library/ColorSync/Profiles/AW3225QF EDID Strict v2.icc"
icc_aw3225qf_edid_strict_v4="${HOME}/Library/ColorSync/Profiles/AW3225QF EDID Strict v4.icc"
icc_s3221qs_standard="${HOME}/Library/ColorSync/Profiles/DELL S3221QS Standard.icc"
icc_s3221qs_edid_strict_v2="${HOME}/Library/ColorSync/Profiles/DELL S3221QS EDID Strict v2.icc"
icc_s3221qs_edid_strict_v4="${HOME}/Library/ColorSync/Profiles/DELL S3221QS EDID Strict v4.icc"

if which gdate &> /dev/null; then
    alias ts='gdate "+%Y-%m-%d %H:%M:%S.%3N"'
else
    alias ts='date "+%Y-%m-%d %H:%M:%S"'
fi

#---------------------------------------------------
# aw3225qf normal settings
#   - factory settinng
#---------------------------------------------------
declare -A aw3225qf_normal_set_0=(
    [hdr]=off
)
declare -A aw3225qf_normal_set_1=(
    [resolution]=2560x1440
    [refreshRate]=48-240Hz
)
declare -A aw3225qf_normal_set_2=(
    [colorProfileURL]="file://${icc_aw3225qf_edid_strict_v4// /%20}"
    [brightness]=0.75
)

#---------------------------------------------------
# normal s3221qs settings
#   - factory settinng
#---------------------------------------------------
declare -A s3221qs_normal_set_0=(
    [hdr]=off
)
declare -A s3221qs_normal_set_1=(
    [resolution]=2560x1440
    [refreshRate]=60Hz
)

# GPU dithering causes flickering
declare -A s3221qs_normal_set_2=(
    [gpuDithering]=off
    [colorProfileURL]="file://${icc_s3221qs_edid_strict_v4// /%20}"
    [brightness]=0.65
)

#---------------------------------------------------
# dolby vision 4K settings
#---------------------------------------------------
declare -A aw3225qf_dolby_vision_4k_set_0=(
    [resolution]=1920x1080
    [refreshRate]=60Hz
)
declare -A aw3225qf_dolby_vision_4k_set_1=(
    [hdr]=on
)
declare -A aw3225qf_dolby_vision_4k_set_2=(
    [connectionMode]=bpc:12+range:limited
)
declare -A aw3225qf_dolby_vision_4k_set_3=(
    [colorProfileURL]="file://${icc_aw3225qf_lldv// /%20}"
    [brightness]=1
)

#---------------------------------------------------
# dolby vision 5K settings
#---------------------------------------------------
declare -A aw3225qf_dolby_vision_5k_set_0=(
    [resolution]=2560x1440
    [refreshRate]=60Hz
)
declare -A aw3225qf_dolby_vision_5k_set_1=(
    [hdr]=on
)
declare -A aw3225qf_dolby_vision_5k_set_2=(
    [connectionMode]=bpc:12+range:limited
)
declare -A aw3225qf_dolby_vision_5k_set_3=(
    [colorProfileURL]="file://${icc_aw3225qf_lldv// /%20}"
    [brightness]=1
)

local netflixURL="https://www.netflix.com"

usage() {
    print -rC1 -- \
          "" \
          "Usage:" \
          "    $this_script:t <-h|--help>             Show this help." \
          "    $this_script:t --setup                 Install dependencies (BetterDisplay.app, betterdisplaycli)." \
          "    $this_script:t --install[=<location>]  Install utility commands to specifid location. default: ~/.local/bin" \
          ""
    exit 0
}

log() {
    print -P -n "%F{green}[$(ts)]%f "
    print "$@"
}

error() {
    print -P -n "%F{red}[$(ts)] Error:%f"
    print "$@"
}

setup() {
    brew update
    brew install --cask betterdisplay
    brew install waydabber/betterdisplay/betterdisplaycli
    brew cleanup
}

betterdisplay() {
    local disp_var=$1
    local prefix=$2
    local -A display=(${(Pkv@)disp_var})


    local i=0
    # bug? betterdisplaycli can't get hdr=off
    local cur_hdr=$(betterdisplaycli get -name="$display[name]" -hdr 2> /dev/null)
    [[ -z $cur_hdr ]] && cur_hdr=off
    log "$display[name]: current hdr state: $cur_hdr"

    while true; do
        local var=${disp_var}_${prefix}_set_$i
        if [[ ! -v "$var" ]]; then
            break
        fi
        local -A params=(${(Pkv@)var})
        local args=()
        local hdr_changed=false
        for key in ${(k)params}; do
            if [[ $key == hdr ]]; then
                if [[ $cur_hdr ==  $params[$key] ]]; then
                    continue
                else
                    hdr_changed=true
                fi
            fi
            args+=(-$key=$params[$key])
        done
        if (( $#args )); then
            if betterdisplaycli set -name="$display[name]" $args[*]; then
                log "$display[name]: succeeded: betterdisplaycli set ${args[*]}"
                $hdr_changed && sleep 1.5 || sleep 0.2
            else
                error "$display[name]: failed: betterdisplaycli set ${args[*]}"
            fi
        fi
        i=$((i + 1))
    done
}

netflix() {
    local disp_var=$1
    local -A display=(${(Pkv@)disp_var})

    betterdisplay "$disp_var" dolby_vision_4k

    osascript <<-EOF
tell application "Safari"
  activate
  set netflix to null
  set startPage to null
  repeat with thisWindow in windows
    repeat with thisTab in tabs of thisWindow
      set thisURL to URL of thisTab
      -- already open netflix ?
      if thisURL contains "$netflixURL" then
        set netflix to {w:thisWindow, t:thisTab}
        exit repeat
      end if
      -- start page ?
      if thisURL is "favorites://" then
        set startPage to {w:thisWindow, t:thisTab}
      end if
    end repeat
    if netflix is not null then exit repeat
  end repeat
  if netflix is null then
    if startPage is null then
      make new document with properties {URL:"$netflixURL"}
      set netflix to {w:front window, t:current tab of front window}
    else
      set URL of t of startPage to "$netflixURL"
      set netflix to startPage
    end if
  end if
  set index of w of netflix to 1
  tell front window
    set current tab to (t of netflix)
    set bounds to $display[bounds_4k]
  end tell
end tell
EOF
}

apple_tv() {
    local disp_var=$1
    local -A display=(${(Pkv@)disp_var})

    betterdisplay $disp_var dolby_vision_4k

    osascript <<-EOF
tell application "TV"
  activate
  set tvWin to null
  repeat with aWin in windows
    if name of aWin is "TV" then
      set tvWin to aWin
    end if
  end repeat
 if tvWin is not null then
   tell tvWin
     set bounds to $display[bounds_4k]
     set visible to true
   end tell
 end if
end tell
EOF
}

reset_displays() {
    betterdisplay aw3225qf normal
    betterdisplay s3221qs normal
}

install() {
    local install_location=$1
    local commmands=(
        apple-tv
        netflix
        dolby-vision-4k
        dolby-vision-5k
        reset-display
    )
    if [[ ! -d $install_location ]]; then
        error "Install location not found: $install_location"
        exit 1
    fi
    if [[ ! -w $install_location ]]; then
        error "Install location not writable: $install_location"
        exit 1
    fi

    for cmd in $commmands; do
        ln -sf $this_script $install_location/$cmd
        log "Installed: $install_location/$cmd"
    done

    # install icc profiles
    cd $PROJECT/icc
    for icc_xml in *.xml; do
        local icc_name=${icc_xml%.xml}
        local icc_path="${HOME}/Library/ColorSync/Profiles/${icc_name}.icc"
        rm -f "$icc_path"
        IccFromXml "$icc_xml" "$icc_path"
        log "Installed: $icc_path"
    done
}

if [[ $command == $(basename $this_script) ]]; then
    zparseopts -D -E -F -- \
               {h,-help}=help \
               -setup=setup \
               -install::=install
    if (( $#help )); then
        usage
    elif (( $#setup )); then
        setup
    elif (( $#install )); then
        local loc=${install##--install}
        loc=${loc##=}
        if [[ -z $loc ]]; then
            loc=~/.local/bin
        fi
        install $loc
    fi
    return
fi

case $command in
    apple-tv )
        apple_tv aw3225qf
        ;;
    netflix )
        netflix aw3225qf
        ;;
    dolby-vision-4k )
        betterdisplay aw3225qf dolby_vision_4k
        ;;
    dolby-vision-5k )
        betterdisplay aw3225qf dolby_vision_5k
        ;;
    reset-display )
        reset_displays
        ;;
esac
