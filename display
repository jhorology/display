#!/bin/zsh -eu

command=$0:t
this_script="$(realpath $0)"
PROJECT=$this_script:h
cd $PROJECT

declare -A display=(
  [name]="AW3225QF"
  [bounds_5k]="{0, 0, 2560, 1440}"
  [bounds_4k]="{0, 0, 1920, 1080}"
)

#---------------------------------------------------
# normal settings
#---------------------------------------------------
declare -A normal_set_0=(
    [hdr]=off
)
declare -A normal_set_1=(
    [resolution]=2560x1440
    [refreshRate]=48-240Hz
)
declare -A normal_set_2=(
    [colorProfileURL]="file:///Library/ColorSync/Profiles/Displays/AW3225QF-D61FFD6E-1D7D-42D9-A314-59D2ED731496.icc"
    [brightness]=0.75
)

#---------------------------------------------------
# dolby vision 4K settings
#---------------------------------------------------
declare -A dolby_vision_4k_set_0=(
    [resolution]=1920x1080
    [refreshRate]=60Hz
)
declare -A dolby_vision_4k_set_1=(
    [hdr]=on
)
declare -A dolby_vision_4k_set_2=(
    [connectionMode]=bpc:12+range:limited
)
declare -A dolby_vision_4k_set_3=(
    [colorProfileURL]="file:///System/Library/ColorSync/Profiles/DCI(P3)%20RGB.icc"
    [brightness]=1
)

#---------------------------------------------------
# dolby vision 4K settings
#---------------------------------------------------
declare -A dolby_vision_5k_set_0=(
    [resolution]=2560x1330
    [refreshRate]=60Hz
)
declare -A dolby_vision_5k_set_1=(
    [hdr]=on
)
declare -A dolby_vision_5k_set_2=(
    [connectionMode]=bpc:12+range:limited
)
declare -A dolby_vision_5k_set_3=(
    [colorProfileURL]="file:///System/Library/ColorSync/Profiles/DCI(P3)%20RGB.icc"
    [brightness]=1
)

local netflixURL="https://www.netflix.com"

usage() {
    print -rC1 -- \
          "" \
          "Usage:" \
          "    $this_script:t <-h|--help>             Show this help." \
          "    $this_script:t --setup                 Install dependencies (BetterDisplay.app, betterdisplaycli)." \
          "    $this_script:t --install[=<location>]  Install utility commands to specifid location. default: ~/.local/bin" \
          ""
    exit 0
}

log() {
    local timestamp=$(date "+%Y-%m-%d %H:%M:%S.%3N")
    print -P "%F{green}[$timestamp]%f $*"
}

error() {
    local timestamp=$(date "+%Y-%m-%d %H:%M:%S.%3N")
    print -P "%F{red}[$timestamp] Error:%f $*"
}

setup() {
    brew update
    brew install --cask betterdisplay
    brew install waydabber/betterdisplay/betterdisplaycli
    brew cleanup
}

betterdisplay() {
    local prefix=$1
    local i=0
    # bug? betterdisplaycli can't get hdr=off
    local cur_hdr=$(betterdisplaycli get -name="$display[name]" -hdr 2> /dev/null)
    [[ -z $cur_hdr ]] && cur_hdr=off
    log "current hdr state: $cur_hdr"

    while true; do
        local var=${prefix}_set_$i
        if [[ ! -v "$var" ]]; then
            break
        fi
        local -A params=(${(Pkv@)var})
        local args=()
        local hdr_changed=false
        for key in ${(k)params}; do
            if [[ $key == hdr ]]; then
                if [[ $cur_hdr ==  $params[$key] ]]; then
                    continue
                else
                    hdr_changed=true
                fi
            fi
            args+=(-$key=$params[$key])
        done
        if (( $#args )); then
            if betterdisplaycli set -name="$display[name]" $args[*]; then
                log "succeeded: betterdisplaycli set ${args[*]}"
                $hdr_changed && sleep 1.5 || sleep 0.2
            else
                error "failed: betterdisplaycli set ${args[*]}"
            fi
        fi
        i=$((i + 1))
    done
}

netflix() {
    betterdisplay dolby_vision_4k

    osascript <<-EOF
tell application "Safari"
  activate
  set netflix to null
  set startPage to null
  repeat with thisWindow in windows
    repeat with thisTab in tabs of thisWindow
      set thisURL to URL of thisTab
      -- already open netflix ?
      if thisURL contains "$netflixURL" then
        set netflix to {w:thisWindow, t:thisTab}
        exit repeat
      end if
      -- start page ?
      if thisURL is "favorites://" then
        set startPage to {w:thisWindow, t:thisTab}
      end if
    end repeat
    if netflix is not null then exit repeat
  end repeat
  if netflix is null then
    if startPage is null then
      make new document with properties {URL:"$netflixURL"}
      set netflix to {w:front window, t:current tab of front window}
    else
      set URL of t of startPage to "$netflixURL"
      set netflix to startPage
    end if
  end if
  set index of w of netflix to 1
  tell front window
    set current tab to (t of netflix)
    set bounds to $display[bounds_4k]
  end tell
end tell
EOF
}

apple_tv() {
    betterdisplay dolby_vision_4k

    osascript <<-EOF
tell application "TV"
  activate
  set tvWin to null
  repeat with aWin in windows
    if name of aWin is "TV" then
      set tvWin to aWin
    end if
  end repeat
 if tvWin is not null then
   tell tvWin
     set bounds to $display[bounds_4k]
     set visible to true
   end tell
 end if
end tell
EOF
}

reset_display() {
    betterdisplay normal
}

install() {
    local install_location=$1

    ln -sf $this_script $install_location/apple-tv
    log "Installed $install_location/apple-tv"
    ln -sf $this_script $install_location/netflix
    log "Installed $install_location/netflix"
    ln -sf $this_script $install_location/dolby-vision-4k
    log "Installed $install_location/dolby-vision-4k"
    ln -sf $this_script $install_location/dolby-vision-5k
    log "Installed $install_location/dolby-vision-5k"
    ln -sf $this_script $install_location/reset-display
    log "Installed $install_location/reset-display"
}


if [[ $command == $(basename $this_script) ]]; then
    zparseopts -D -E -F -- \
               {h,-help}=help \
               -setup=setup \
               -install::=install
    if (( $#help )); then
        usage
    elif (( $#setup )); then
        setup
    elif (( $#install )); then
        local loc=${install##--install}
        loc=${loc##=}
        if [[ -z $loc ]]; then
            loc=~/.local/bin
        fi
        install $loc
    fi
    return
fi

case $command in
    apple-tv )
        apple_tv
        ;;
    netflix )
        netflix
        ;;
    dolby-vision-4k )
        betterdisplay dolby_vision_4k
        ;;
    dolby-vision-5k )
        betterdisplay dolby_vision_5k
        ;;
    reset-display )
        reset_display
        ;;
esac
